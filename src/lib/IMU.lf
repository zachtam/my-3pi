/**
 * Library reactors for the IMU on the 
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * Note that these reactors rely on the fact that they are executed in an unthreaded
 * runtime. To adapt them for a threaded runtime, they will need to use mutual
 * exclusion to ensure that multiple instances of the reactors are not simultaneously
 * accessing the shared hardware resource.
 * 
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  threading: false,
  files: ["../../robot-lib/"],                      // Needed to find imu.h
  cmake-include: ["../../robot-lib/robot-lib.txt"]  // Needed to find imu.h
}

preamble {=
  #include <pico/stdlib.h>
  #include <imu.h>
  #define RAD2DEG 57.2957795131f
=}

/**
 * Base class for IMU reactors.
 */
reactor IMUBase {
  preamble {=
    // Static global IMU instance struct.
    static imu_inst_t imu_instance;
    // Boolean to ensure the IMU instance is initialized only once.
    static bool _pololu_robot_imu_initialized = false;
  =}

  input trigger:bool;
    
  output x:float;
  output y:float;
  output z:float;

  reaction(startup) {=
    if (!_pololu_robot_imu_initialized) {
      _pololu_robot_imu_initialized = true;
      imu_init(&imu_instance);
    }
  =}
}

/**
 * When triggered by an input (whose value is ignored), sample the accelerometer
 * on the <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * The accelerometer is provided by an
 * <a href="https://www.pololu.com/file/0J1899/lsm6dso.pdf">ST LMS6DSO inertial measurement unit</a>.
 * The outputs give acceleration in <i>g</i>'s with a range of -2 to +2 <i>g</i>
 * and a sensitivity of approximately 0.061 <i>mg</i>'s.
 */
reactor Accelerometer extends IMUBase {  
  reaction(trigger) -> x, y, z {= 
    axes_data_t acc_data;
    imu_read_acc(&imu_instance, &acc_data);
    float mx, my, mz;
    float bx, by, bz;
    bx = 0.0;
    mx = 1.001 - bx;
    by = -0.016;
    my = 1.009 - by;
    bz = 0.0;
    mz = 1.031 - bz;
    lf_set(x, (acc_data.x - bx) / mx);
    lf_set(y, (acc_data.y - by) / my);
    lf_set(z, (acc_data.z - bz) / mz);   
    // lf_set(x, acc_data.x);
    // lf_set(y, acc_data.y);
    // lf_set(z, acc_data.z);   
  =}
}

/**
 * When triggered by an input (whose value is ignored), sample the gyroscope
 * on the <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * The gyroscope is provided by an
 * <a href="https://www.pololu.com/file/0J1899/lsm6dso.pdf">ST LMS6DSO inertial measurement unit</a>.
 * The outputs give angular velocity in degrees per second.
 */
 reactor Gyro extends IMUBase {
  reaction(trigger) -> x, y, z {=
    axes_data_t gyro_data;
    imu_read_gyro(&imu_instance, &gyro_data);
    lf_set(x, gyro_data.x);
    lf_set(y, gyro_data.y);
    lf_set(z, gyro_data.z);
  =}
}

reactor Magnetometer extends IMUBase {
  reaction(trigger) -> x, y, z {=
    axes_data_t mag_data;
    imu_read_mag(&imu_instance, &mag_data);
    lf_set(x, mag_data.x);
    lf_set(y, mag_data.y);
    lf_set(z, mag_data.z);
  =}
}

reactor CorrectedMag extends IMUBase {
  reaction(trigger) -> x, y, z {=
    axes_data_t mag_data;
    imu_read_mag(&imu_instance, &mag_data);

    // from magneto.c from https://forum.pololu.com/t/correcting-the-balboa-magnetometer/14315
    // float B {  -64.84,   -6.79,  -32.61};

    // float Ainv {{ 37.37062,  1.49009, -1.22506},
    //             {  1.49009, 40.46546,  0.47475},
    //             { -1.22506,  0.47475, 43.94860}};

    float x_centered = mag_data.x - (-64.84);
    float y_centered = mag_data.y - (-6.79);
    float z_centered = mag_data.z - (-32.61);
    float x_corr =   37.37062*x_centered +  1.49009*y_centered + (-1.22506)*z_centered;
    float y_corr =    1.49009*x_centered + 40.46546*y_centered +    0.47475*z_centered;
    float z_corr = (-1.22506)*x_centered +  0.47475*y_centered +   43.94860*z_centered;
    lf_set(x, x_corr);
    lf_set(y, y_corr);
    lf_set(z, z_corr);
  =}
}

/**
 * Integrate the sequence of inputs using the trapezoidal method.
 * The first output is always zero.  Subsequent outputs are the
 * sum of the current and previous inputs multiplied by the time
 * interval since the last input (in seconds) divided by two.
 */
reactor TrapezoidalIntegrator {
  input in:float
  output out:float
  reset state s:float = 0
  reset state prev_in:float = 0
  reset state prev_time:instant_t = 0
  reaction(in) -> out {=
    instant_t now = lf_time_logical();
    if (self->prev_time > SEC(0)) {
      float interval = ((now - self->prev_time) * 1e-9);
      self->s += (in->value + self->prev_in) * interval / 2;
    }
    lf_set(out, self->s);
    self->prev_in = in->value;
    self->prev_time = now;
  =}
}

/**
 * When triggered by an input (whose value is ignored), sample the gyroscope
 * on the <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>
 * and integrate its output.  The output is an estimate of the angle (in degrees)
 * of the robot relative to its angle when first triggered along each of three axes.
 * The gyroscope is provided by an
 * <a href="https://www.pololu.com/file/0J1899/lsm6dso.pdf">ST LMS6DSO inertial measurement unit</a>.
 */
 reactor GyroAngle {
  input trigger:bool;
    
  output x:float;
  output y:float;
  output z:float;

  g = new Gyro()
  integrator1 = new TrapezoidalIntegrator()
  integrator2 = new TrapezoidalIntegrator()
  integrator3 = new TrapezoidalIntegrator()

  trigger -> g.trigger
  g.x, g.y, g.z -> integrator1.in, integrator2.in, integrator3.in
  integrator1.out, integrator2.out, integrator3.out -> x, y, z
}


reactor MagGyroAngle {
  preamble {=
    const float rho = 0.01;
  =}

  input trigger:bool;

  output heading:float

  g = new Gyro()
  m = new CorrectedMag()

  (trigger)+ -> g.trigger, m.trigger
  reset state heading_internal:float = 0
  reset state prev_gyro:float = 0
  reset state prev_time:instant_t = 0
  reaction(m.x, m.y, g.z) -> heading {=
    instant_t now = lf_time_logical();
    if (self->prev_time > SEC(0)) {
      float interval = ((now - self->prev_time) * 1e-9);
      float heading_predict =
        self->heading_internal + (g.z->value + self->prev_gyro) * interval / 2;
      float heading_correct =
        atan2(-m.y->value, m.x->value)*RAD2DEG;
      while (heading_correct - heading_predict > 180.0) {
        heading_correct -= 360.0;
      }
      while (heading_correct - heading_predict < -180.0) {
        heading_correct += 360.0;
      }
      self->heading_internal = (1 - rho)*heading_predict + rho*heading_correct;
    } else {
      self->heading_internal = atan2(-m.y->value, m.x->value)*RAD2DEG;
    }
    lf_set(heading, self->heading_internal);
    self->prev_gyro = g.z->value;
    self->prev_time = now;
  =}
}
